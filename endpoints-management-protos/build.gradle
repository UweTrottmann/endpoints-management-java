plugins {
  id("ut-publish")
  id("com.google.protobuf") version "0.9.4"
}

version = "1.0.15-SNAPSHOT"

// Set project-specific publishing properties
publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "endpoints-management-protos"
      from components.java
      pom {
        name.set("Endpoints Management - Generated Java Protobufs")
        description.set("Contains the generated protocol buffer surface used by Endpoints")
      }
    }
  }
}

repositories {
  mavenCentral()
  mavenLocal()
}

dependencies {
  api platform(libraries.protobufJavaBom)
  api libraries.protobufJava

  api platform(libraries.grpcBom)
  api libraries.grpcStub
  api libraries.grpcProtobuf
}

protobuf {
  protoc {
    // The version of protoc must match protobuf-java. If you don't depend on
    // protobuf-java directly, you will be transitively depending on the
    // protobuf-java version that grpc depends on.
    artifact = "com.google.protobuf:protoc:${protobufJavaVersion}"
  }
  plugins {
    grpc {
      artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
    }
  }
  generateProtoTasks {
    all()*.plugins {
      grpc {
        outputSubDir = 'java'
      }
    }
  }
}

sourceSets {
  main {
    java {
      srcDir 'build/generated/source/proto/main/java'
    }
    proto {
      setIncludes([
              "google/api/servicecontrol/v1/*.proto",
              "google/api/distribution.proto",
              "google/api/client.proto",
              "google/api/launch_stage.proto"
      ])
    }
  }
}
