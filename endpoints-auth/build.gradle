/*
 * Copyright 2016 Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id("endpoints-management-java.java-conventions")
  id("endpoints-management-java.checkstyle-conventions")
  id("endpoints-management-java.publish-conventions")
}

version = "1.0.15"

// Set project-specific publishing properties
publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "endpoints-management-auth"
      from components.java
      pom {
        name.set("Endpoints API Management - Authentication")
        description.set("Enables authentication by multiple authentication providers")
      }
    }
  }
}

configurations {
  integrationTestCompile.extendsFrom testImplementation
  integrationTestRuntime.extendsFrom testRuntimeOnly
}

dependencies {
  implementation project(":endpoints-service-config")
  api(project(":endpoints-control")) {
    transitive = false // Just importing a model file (MethodRegistry.AuthInfo), do not pull in all dependencies.
  }

  implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
  api "com.google.guava:guava:${guavaVersion}"
  implementation "com.google.flogger:flogger:${floggerVersion}"
  runtimeOnly "com.google.flogger:flogger-system-backend:${floggerVersion}"
  api "com.google.http-client:google-http-client:${httpClientVersion}"
  api "javax.servlet:servlet-api:${servletApiVersion}"
  api "org.bitbucket.b_c:jose4j:${jose4jVersion}"

  testImplementation "junit:junit:${junitVersion}"
  testImplementation "org.bouncycastle:bcmail-jdk15on:${bouncycastleVersion}"
  testImplementation "org.mockito:mockito-core:${mockitoVersion}"

  testImplementation "org.apache.commons:commons-lang3:${commonsLang3Version}"
  testImplementation "org.eclipse.jetty:jetty-server:${jettyVersion}"
  testImplementation "org.eclipse.jetty:jetty-servlet:${jettyVersion}"
  testImplementation "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
  testImplementation "org.glassfish.jersey.containers:jersey-container-servlet-core:${jerseyVersion}"
}

sourceSets {
  integrationTest {
    java {
      compileClasspath += main.output + test.output
      runtimeClasspath += main.output + test.output
      srcDir file("src/integration-test/java")
    }
    resources.srcDir file("src/integration-test/resources")
  }
}

task integrationTest(type: Test) {
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test
