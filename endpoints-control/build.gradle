/*
 * Copyright 2016 Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id("ut-publish")
}

version = "1.0.15-SNAPSHOT"

// Set project-specific publishing properties
publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = "endpoints-management-control"
      from components.java
      pom {
        name.set("Endpoints API Management")
        description.set("Provide access control for managed services")
      }
    }
  }
}

test {
  testLogging {
    exceptionFormat = 'full'
    testLogging.showStandardStreams = true
  }
}

sourceSets {
  test {
    resources.srcDir file("src/test/resources")
  }
}

processResources {
  filesMatching('**/version.properties') {
    expand 'serviceControlVersion': project.findProperty("version") ?: "UNKNOWN"
  }
}

dependencies {
  implementation "com.google.auto.value:auto-value-annotations:${autoValueVersion}"
  annotationProcessor "com.google.auto.value:auto-value:${autoValueVersion}"
  api "com.google.code.findbugs:jsr305:${jsr305Version}"
  implementation "com.google.flogger:flogger:${floggerVersion}"
  runtimeOnly "com.google.flogger:flogger-system-backend:${floggerVersion}"
  api "com.google.guava:guava:${guavaVersion}"
  implementation "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
  implementation("com.google.http-client:google-http-client-jackson2:${httpClientVersion}") {
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    exclude group: 'com.google.guava', module: 'guava-jdk5'
  }
  implementation("com.google.api-client:google-api-client:${googleApiClientProtobufVersion}") {
    exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
    exclude group: 'com.google.guava', module: 'guava-jdk5'
  }
  api "javax.servlet:servlet-api:${servletApiVersion}"
  api project(":endpoints-control-api-client")
  implementation project(":endpoints-service-config")
  api project(":endpoints-management-protos")

  testImplementation "junit:junit:${junitVersion}"
  testImplementation "com.google.truth:truth:${truthVersion}"
  testImplementation "org.mockito:mockito-core:${mockitoVersion}"
  testImplementation "com.google.protobuf:protobuf-java-util"
}
